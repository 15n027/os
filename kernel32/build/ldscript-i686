ENTRY(_start)
OUTPUT_FORMAT(elf32-i386)

PHDRS
{
        text PT_LOAD FILEHDR PHDRS;
        data PT_LOAD;
}

SECTIONS
{
        . = 1M + SIZEOF_HEADERS;
        _start_kernel = .;
        .multiboot : {
             *(.multiboot)
        } :text
        . = .;
        _stext = .;
        .text : {
              *(.text*)
              *(.gnu.linkonce.r.* .rodata .rodata*);
        }
        IDTData : {
              *(IDT*);
        }
        _etext = .;
        . = ALIGN(0x1000);
        _s_rw_area = .;
        _sdata = .;
        .data : {
              *(.data*)
              _edata = ABSOLUTE(.);
        } :data
        . = ALIGN(64);
        . = .;
        _sbss = .;
        .bss : {
              *(.dynbss .bss .bss* COMMON .gnu.linkonce.b.*)
              . = ALIGN(64);
              _stack0_bottom = ABSOLUTE(.);
              . += 0x1000;
              _stack0_top = ABSOLUTE(.);
        }
        _ebss = ABSOLUTE(.);
        _bss_size = SIZEOF(.bss);
        _end_kernel = .;
}
