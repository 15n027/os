ENTRY(_start)
OUTPUT_FORMAT(elf64-x86-64)

PHDRS
{
        text PT_LOAD FILEHDR PHDRS;
        data PT_LOAD;
}

SECTIONS
{
        . = 0xffffffff80000000 + SIZEOF_HEADERS;
        . = .;
        _start_kernel = .;
        _stext = .;
        . = .;
        .text : {
              *(.text*)
              _etext = ABSOLUTE(.);
              *(.gnu.linkonce.r.* .rodata .rodata*);
        } :text
        IDTData : {
              *(IDTData);
        } :text
        _sctors = .;
        .ctors : {
              KEEP(*(.ctors));
              KEEP(*(.ctors.*));
              KEEP(*(.init_array));
              KEEP(*(.preinit_array))
              _ectors = ABSOLUTE(.);
        }
        . = ALIGN(0x1000);
        . = .;
        .data : {
              *(.data*)
              _edata = ABSOLUTE(.);
        } :data

        . = ALIGN(256);
        _sbss = ABSOLUTE(.);
        .bss : {
              *(.dynbss .bss .bss* COMMON .gnu.linkonce.b.*)
              _bss_size = ABSOLUTE(SIZEOF(.bss));
              . = ALIGN(0x1000);
              _stack0_guard = ABSOLUTE(.);
              . += 4K;
              _stack0_bottom = ABSOLUTE(.);
              . += 8K;
              _stack0_top = ABSOLUTE(.);
              _stack0_size = ABSOLUTE(_stack0_top - _stack0_bottom);
              _ebss = ABSOLUTE(.);
        } :data
        _end_kernel= .;
}
