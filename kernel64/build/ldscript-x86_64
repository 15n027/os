ENTRY(_start)
OUTPUT_FORMAT(elf64-x86-64)

PHDRS
{
        text PT_LOAD FILEHDR PHDRS;
        data PT_LOAD;
}

SECTIONS
{
        . = 0xffffffff80000000 + SIZEOF_HEADERS;
        . = .;
        _start_kernel = .;
        _stext = .;
        . = .;
        .text : {
              *(.text*);
              _etext = ABSOLUTE(.);
              *(.gnu.linkonce.r.* .rodata .rodata*);
              KEEP(*(IDTStubs));
        } :text
        .eh_frame : {
              *(.eh_frame);
        } :text
        . = ALIGN(0x1000);
        IDTData : {
              *(IDTData);
        } :text
        .data : {
              *(.data*)
              _edata = ABSOLUTE(.);
        } :data

        _sbss = ABSOLUTE(.);
        .bss : {
              *(.dynbss .bss .bss* COMMON .gnu.linkonce.b.*)
              _bss_size = ABSOLUTE(SIZEOF(.bss));
              . = ALIGN(0x1000);
              _stack0_guard = ABSOLUTE(.);
              . += 4K;
              _stack0_bottom = ABSOLUTE(.);
              . += 8K;
              _stack0_top = ABSOLUTE(.);
              _stack0_size = ABSOLUTE(_stack0_top - _stack0_bottom);
              _ebss = ABSOLUTE(.);
        } :data
        _end_kernel= .;
}
