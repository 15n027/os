ENTRY(_start)
OUTPUT_FORMAT(elf64-x86-64)

PHDRS
{
        text PT_LOAD FILEHDR PHDRS;
        data PT_LOAD;
}

SECTIONS
{
        . = 0xffffffff80000000 + SIZEOF_HEADERS;
        . = .;
        _start_kernel = .;
        _stext = .;
        . = .;
        .text : {
              *(.text*)
              _etext = ABSOLUTE(.);
              *(.gnu.linkonce.r.* .rodata .rodata*);
        } :text

        _sctors = .;
        .ctors : {
              KEEP(*(.ctors));
              KEEP(*(.ctors.*));
              KEEP(*(.init_array));
              KEEP(*(.preinit_array))
              _ectors = ABSOLUTE(.);
        }
        . = ALIGN(0x1000);
        . = .;
        .data : {
              *(.data*)
              _edata = ABSOLUTE(.);
        } :data

        . = ALIGN(64);
        _sbss = .;
        .bss : {
              *(.dynbss .bss .bss* COMMON .gnu.linkonce.b.*)
              . = ALIGN(0x1000);
              . += 8K;
              _stack = ABSOLUTE(.);
        } :data
        
        _ebss = ABSOLUTE(.);
        _bss_size = SIZEOF(.bss);
        _end_kernel = .;        
}
