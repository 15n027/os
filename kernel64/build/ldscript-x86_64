ENTRY(_start)
OUTPUT_FORMAT(elf64-x86-64)

PHDRS
{
        text PT_LOAD FILEHDR PHDRS;
        data PT_LOAD;
}

SECTIONS
{
        . = 0xfffffffff0000000 + SIZEOF_HEADERS;
        . = .;
        _stext = .;
        .text : {
              *(.text*)
              _etext = ABSOLUTE(.);
              *(.gnu.linkonce.r.* .rodata .rodata*);
        } :text

        _sctors = .;
        .ctors : {
              KEEP(*(.ctors));
              KEEP(*(.ctors.*));
              KEEP(*(.init_array));
              KEEP(*(.preinit_array))
              _ectors = ABSOLUTE(.);
        }
        . = ALIGN(0x1000);
        .data ALIGN(0x1000) : {
              *(.data*)
              _edata = ABSOLUTE(.);
        } :data

        . = ALIGN(64);
        _sbss = .;
        .bss : {
              *(.dynbss .bss .bss* COMMON .gnu.linkonce.b.*)
              
        }
        _ebss = ABSOLUTE(.);
        _bss_size = SIZEOF(.bss);
        _end_kernel = .;
}
